import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator'
    id 'maven-publish'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // ./gradlew apiCheck clean testReleaseUnitTest lintRelease pixel2api30Check
                from components.release
                groupId = 'com.github.popovanton0.kira'
                artifactId = 'kira'
                version = 'SNAPSHOT2'

//                pom {
//                    name = "kira"
//                    description = "Automatically generates UI demos which allow users to call any function with any parameters"
//                    url = 'https://github.com/popovanton0/kira'
//                    licenses {
//                        license {
//                            name = 'The Apache License, Version 2.0'
//                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
//                        }
//                    }
//                    developers {
//                        developer {
//                            id = 'popovanton0'
//                            name = 'Anton Popov'
//                            email = 'popovanton0+github@gmail.com'
//                        }
//                    }
//                }
            }
        }

        repositories {
            def mavenDeployDir = System.env.DEPLOY_DIR ?: "${rootProject.buildDir}/repo"
            maven {
                url mavenDeployDir
            }
        }
    }
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ['-Xexplicit-api=strict', '-opt-in=kotlin.RequiresOptIn']
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    namespace 'com.popovanton0.kira'

    testOptions {
        managedDevices {
            devices {
                pixel2api30(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    // Use only API levels 27 and higher.
                    apiLevel = 30
                    systemImageSource = "aosp"
                    require64Bit = false
                }
            }
        }
    }
}

dependencies {
    api project(":kira-annotations")

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
